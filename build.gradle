buildscript {
    ext {
        kotlinVersion = '1.3.30'
        elasticsearchVersion = '7.0.0'
        junitVersion = '5.4.1'
        jacksonVersion = '2.9.8'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.18"
        classpath "com.avast.gradle:gradle-docker-compose-plugin:0.9.1"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.21.0' // gradle dependencyUpdates -Drevision=release
        classpath "de.thetaphi:forbiddenapis:2.6"
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:3.21.1'
    }
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'docker-compose'
apply plugin: 'project-report' // useful for gradle htmlDependencyReport
apply plugin: 'com.diffplug.gradle.spotless'
apply plugin: "de.thetaphi.forbiddenapis"
apply plugin: 'com.github.ben-manes.versions'

forbiddenApis {
    // https://github.com/policeman-tools/forbidden-apis/wiki/GradleUsage
    bundledSignatures = ["jdk-unsafe-9", "jdk-deprecated-9", "jdk-non-portable", "jdk-internal-9"]
    // take out "jdk-system-out"
    signaturesFiles = files("forbidden_signatures.txt")
    ignoreFailures = false
}

// this intentionally breaks the build if you mess up the formatting; pro tip: use Save Actions plugin in intellij and auto format and organize imports on save
spotless {
    java {
        removeUnusedImports() // removes any unused imports
    }
    // to fix violations: gradle spotlessApply
    // in intellij also tweak kotlin imports to require 999 imports before wildcards and remove java.util for always using wildcards
    // do the same for java imports
    // wildcards are verboten!
    // currentlyb breaks offline build due to fucked up transitive dependency on ktlint & kotlin
    kotlin {
        // optionally takes a version
        ktlint()
    }
}

afterEvaluate {
    // just ffing run it, way to anal to break the build every time
    tasks.getByName('spotlessCheck').dependsOn(tasks.getByName('spotlessApply'))
}

dokka {
    outputFormat = 'gfm'
    jdkVersion = 8
    outputDirectory = 'docs'
    includes = ['src/main/kotlin/io/inbot/eskotlinwrapper/module.md']
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        // for whatever reason defaults to 1.6. But since we don't care about android ...
        jvmTarget = "1.8"
    }
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
}

test {
    useJUnitPlatform()
    testLogging {
        // Make sure output from
        // standard out or error is shown
        // in Gradle output.
        exceptionFormat = 'full'
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

dockerCompose.isRequiredBy(test)

dockerCompose {
    buildAdditionalArgs = ['--force-rm']
    forceRecreate = true
}

group = 'io.inbot.search'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        // for whatever reason defaults to 1.6. But since we don't care about android ...
        jvmTarget = "1.8"
        // zero tolerance on warnings
        allWarningsAsErrors = true
        freeCompilerArgs = ["-Xjsr305=strict"]
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

repositories {
    mavenCentral()
    maven { url 'https://dl.bintray.com/kotlin/dokka' }
}


dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.2.0'
    compile "io.github.microutils:kotlin-logging:1.6.26"

    compile "org.apache.commons:commons-lang3:3.9"

    compile "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"

    compile "org.elasticsearch:elasticsearch:$elasticsearchVersion"
    compile "org.elasticsearch.client:elasticsearch-rest-high-level-client:$elasticsearchVersion"

    // bring your own logging, but we need some in tests
    testCompile "org.slf4j:slf4j-api:1.7.25"
    testCompile "org.slf4j:jcl-over-slf4j:1.7.25"
    testCompile "org.slf4j:log4j-over-slf4j:1.7.25"
    testCompile "org.slf4j:jul-to-slf4j:1.7.25"
    testCompile "org.apache.logging.log4j:log4j-to-slf4j:2.11.2" // es seems to insist on log4j2
    testCompile "ch.qos.logback:logback-classic:1.2.3"

    testCompile "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testCompile "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"

    testCompile "io.mockk:mockk:1.9.3"
    testCompile 'com.willowtreeapps.assertk:assertk:0.10'

}
